apply plugin: 'com.android.application'

def Properties mVersionProps = getVersionProperties()

def mVersionCode = getVersionCode()

def mVersionName = mVersionProps['MAJOR_VERSION_NAME'] + "." + mVersionCode


android {
    compileSdkVersion 27
    buildToolsVersion '28.0.2'
    defaultConfig {
        applicationId "org.mazhuang.guanggoo"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode mVersionCode
        versionName mVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [
                appChannel: "unknown"
        ]
    }

    flavorDimensions 'channel'

    productFlavors {
        playstore {}
        github {}
        coolapk {}
        qq {}
        xiaomi {}
        three60 {}
    }

    productFlavors.all {
        flavor ->
            flavor.manifestPlaceholders = [
                    appChannel: name
            ]
    }

    signingConfigs {
        debug {

        }
        release {
            def Properties signingProps = getSignProperties()
            if (signingProps != null) {
                storeFile file(signingProps['STORE_FILE'])
                storePassword signingProps['STORE_PASSWORD']
                keyAlias signingProps['KEY_ALIAS']
                keyPassword signingProps['KEY_PASSWORD']
            }
        }
    }

    buildTypes {
        debug {

        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (getSignProperties() != null) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "guanggoo_${variant.productFlavors[0].name}_v${defaultConfig.versionName}_${variant.buildType.name}.apk"
        }
    }
}

def getVersionProperties() {
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('../config/version.properties')))
    return versionProps
}

static def getVersionCode() {
    try {
        String output = 'git rev-list HEAD --count '.execute().text.trim()
        return Integer.valueOf(output);
    } catch (Exception e) {
        System.out.println(e.getMessage());
    }
    return 1;
}

def getSignProperties() {
    File signingPropertiesFile = file('../config/signing.properties')
    if (signingPropertiesFile.exists()) {
        def Properties signingProperties = new Properties()
        signingProperties.load(new FileInputStream(signingPropertiesFile))
        if (signingProperties.containsKey('STORE_FILE') &&
                signingProperties.containsKey('STORE_PASSWORD') &&
                signingProperties.containsKey('KEY_ALIAS') &&
                signingProperties.containsKey('KEY_PASSWORD')) {
            return signingProperties
        }
    }
    return null
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'org.jsoup:jsoup:1.10.3'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'org.sufficientlysecure:html-textview:3.5'
    implementation 'com.tencent.bugly:crashreport_upgrade:1.3.5'
    testImplementation 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.github.medyo:android-about-page:1.2.1'
    implementation('com.vdurmont:emoji-java:4.0.0') { exclude group: 'org.json', module: 'json' }
    implementation 'com.bm.photoview:library:1.4.1'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    compileOnly 'org.projectlombok:lombok:1.18.2'
    annotationProcessor 'org.projectlombok:lombok:1.18.2'
}
